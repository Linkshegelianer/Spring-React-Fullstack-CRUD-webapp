plugins {
	id 'application'
	id 'checkstyle'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
	id 'org.siouan.frontend-jdk11' version '6.0.0'
	id 'jacoco'
}

group = 'project.code'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 20

application {
	mainClass = 'project.code.AppApplication'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-security',
			'org.liquibase:liquibase-core',
			'org.springdoc:springdoc-openapi-ui:1.7.0',
			'io.jsonwebtoken:jjwt:0.9.1',
			'org.springdoc:springdoc-openapi-ui:1.7.0',
			'com.rollbar:rollbar-spring-boot3-webmvc:1.10.0',
			'com.querydsl:querydsl-jpa:5.0.0:jakarta',
			'io.jsonwebtoken:jjwt-api:0.11.5',
			'org.springframework.boot:spring-boot-starter-test'
	)

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly (
			'com.h2database:h2',
			'org.postgresql:postgresql:42.6.0',
			'io.jsonwebtoken:jjwt-impl:0.11.5',
			'io.jsonwebtoken:jjwt-jackson:0.11.5'
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core',
			'info.picocli:picocli:4.7.1',
			'org.liquibase.ext:liquibase-hibernate6:4.21.1',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'com.h2database:h2',
			'org.postgresql:postgresql:42.5.4',
			'org.liquibase:liquibase-groovy-dsl:3.0.2',
			'org.liquibase.ext:liquibase-hibernate5:4.16.1',
			"ch.qos.logback:logback-core:1.4.6",
			"ch.qos.logback:logback-classic:1.4.6"
	)

	annotationProcessor(
			'org.projectlombok:lombok',
			'com.querydsl:querydsl-apt:5.0.0:jakarta',
			'jakarta.persistence:jakarta.persistence-api:3.1.0',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
			'javax.annotation:javax.annotation-api:1.3.2'
	)
}

tasks.named('test') {
	useJUnitPlatform()
}

compileJava {
	options.release = 20
	options.encoding = 'UTF-8'
}

diffChangelog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changelogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			url 'jdbc:h2:./database73'
		}
	}
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend
